# QuickBMS script for Epic Mickey PAK files
# WRS (xentax.com)
# SOURCE: http://forum.xentax.com/viewtopic.php?f=10&t=5529
endian big
comtype zlib

## header
get H_SIGNATURE long # probally not written as string
get H_VERSION long   # assumed
get H_ZERO    long
get H_SIZE    long
get H_DATAPTR long

math H_DATAPTR += H_SIZE
goto H_SIZE

## file info
get FILES long

math STRPTR = FILES     # calculate string table pos
math STRPTR *= 24       #
savepos FILEDATA        # or += H_SIZE
math STRPTR += FILEDATA #    += 4 (for FILES)

math TOTPOS = H_DATAPTR # sum of aligned size

for f = 0 < FILES

  # record header
  get UNSIZE long # real size
  get PKSIZE long # compressed size
  get ALSIZE long # aligned size

  get FOLDERNM long # string table pointer
  getdstring FILETYPE 4
  get FILENM long # string table pointer

  # get whole filename
  savepos POS

  math FOLDERNM += STRPTR
  math FILENM += STRPTR

  goto FOLDERNM
  get FOLDERSTR string
  goto FILENM
  get FILESTR string

  string FOLDERSTR += "/"
  string FOLDERSTR += FILESTR

  if UNSIZE == PKSIZE
    log FOLDERSTR TOTPOS PKSIZE
  else
    clog FOLDERSTR TOTPOS PKSIZE UNSIZE
  endif

  goto POS
  math TOTPOS += ALSIZE

next f
